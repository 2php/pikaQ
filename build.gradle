/*
 * Copyright (C) 2015 knightliao, Inc. All Rights Reserved.
 */
ext {
    springVersion = "4.2.0.RELEASE"
    springDataJpaVersion = "1.7.1.RELEASE"
    springDataMongodbVersion = "1.6.2.RELEASE"
    springAmqpVersion = '1.4.5.RELEASE'
    jacksonVersion = "2.5.0"
    woodstoxVersion = "4.4.1"
    hibernateVersion = "4.3.7.Final"
    httpclientVersion = "4.3.6"
    log4jVersion = "1.2.17"
    slf4jVersion = "1.7.10"
    junitVersion = '4.10'
    jspVersion = '2.0'
    servletVersion = '2.5'
    cglibVersion = '2.2.2'
}


subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    /*apply plugin: 'findbugs'*/
    apply plugin: 'maven'
    apply plugin: 'idea'

    group = 'com.baidu.pikaq'
    version = '0.0.2-SNAPSHOT'

    eclipse {
        classpath {
            downloadSources = true
        }
    }

    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    tasks.withType(Javadoc) { options.encoding = 'UTF-8' }

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    buildscript {
        repositories {
            mavenLocal()
            maven { url 'http://maven.oschina.net/content/groups/public/' }
            mavenCentral();
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
            maven { url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/' }
        }
        configurations.all {
            resolutionStrategy {
                cacheDynamicVersionsFor 10, 'seconds'
                cacheChangingModulesFor 0, 'seconds'
            }
        }
    }

    repositories {
        mavenLocal()
        maven { url 'http://maven.oschina.net/content/groups/public/' }
        mavenCentral();
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/' }
    }

    dependencies {
        compile "org.springframework:spring-context:${springVersion}"

        compile 'ch.qos.logback:logback-core:1.0.9'
        compile 'ch.qos.logback:logback-classic:1.0.9'
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        testCompile "junit:junit:${junitVersion}"
        testCompile "org.springframework:spring-test:${springVersion}"
    }

    apply from: "${parent.projectDir.canonicalPath}/files/cobertura.gradle"

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/resources'
            }
        }
        test {
            java {
                srcDir 'src/test/java'
            }
            resources {
                srcDir 'src/test/resources'
            }
        }
    }

    dependencies {

    }

    jar {
        manifest {
            attributes 'Implementation-Title': name, 'Implementation-Version': version
        }
    }

    /*   findbugs {
           toolVersion = "2.0.1"
           sourceSets = [sourceSets.main]
           ignoreFailures = true
       }*/

    uploadArchives {
        repositories {
            mavenLocal()
        }
    }
}

project('pikaq-core') {

    dependencies {
        testCompile "org.hsqldb:hsqldb:2.3.1"

        compile "org.springframework:spring-jdbc:${springVersion}"

        compile "commons-lang:commons-lang:2.6"
        compile 'commons-io:commons-io:2.4'

        compile "org.aspectj:aspectjtools:1.7.4"
        compile "org.aspectj:aspectjrt:1.6.11"

        compile "cglib:cglib:${cglibVersion}"
    }

    defaultTasks 'clean', 'test', 'uploadArchives'
}

project('pikaq-client') {
    dependencies {
        compile project(':pikaq-core')

        compile("org.springframework.amqp:spring-amqp:${springAmqpVersion}") {
            exclude group: 'org.springframework', module: 'spring-core'
        }
        compile("org.springframework.amqp:spring-rabbit:${springAmqpVersion}") {
            exclude group: 'org.springframework', module: 'spring-core'
            exclude group: 'org.springframework', module: 'spring-messaging'
            exclude group: 'org.springframework', module: 'spring-tx'
            exclude group: 'org.springframework', module: 'spring-context'
        }
        compile "org.springframework:spring-beans:${springVersion}"
        compile "org.springframework:spring-context-support:${springVersion}"
        compile "org.springframework:spring-core:${springVersion}"
        compile "org.springframework:spring-jdbc:${springVersion}"

        compile "com.google.guava:guava:14.0"

        compile "org.codehaus.jackson:jackson-core-asl:1.9.8"
        compile "org.codehaus.jackson:jackson-mapper-asl:1.9.8"

        testCompile project(':pikaq-core')
        testCompile "com.h2database:h2:1.3.174"
        testCompile 'javax.servlet:javax.servlet-api:3.1.0'
        testCompile 'com.baidu.jorm:jorm:0.0.1-SNAPSHOT'
    }

    defaultTasks 'clean', 'test', 'uploadArchives'
}

defaultTasks 'clean', 'jar', 'test', 'uploadArchives'